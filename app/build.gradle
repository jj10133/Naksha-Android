plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.plugin.compose"
    id "org.jetbrains.kotlin.plugin.serialization"
}

android {
    namespace = "com.foss.naksha.android"
    compileSdk = 35
    ndkVersion = "27.2.12479018"

    defaultConfig {
        applicationId = "com.foss.naksha.android"
        minSdk = 33
        targetSdk = 35
        versionCode = 1
        versionName = "1.0"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    sourceSets {
        main {
            jniLibs.srcDirs "src/main/addons", "libs/bare-kit/jni"
        }
    }
}

tasks.register("link", Exec) {
    workingDir ".."

    commandLine([
            "node_modules/.bin/bare-link",
            "--target", "android-arm",
            "--target", "android-arm64",
            "--target", "android-ia32",
            "--target", "android-x64",
            "--needs", "libbare-kit.so",
            "--out", "app/src/main/addons"
    ])
}

tasks.register("packApp", Exec) {
    workingDir ".."

    commandLine([
            "node_modules/.bin/bare-pack",
            "--target", "android-arm",
            "--target", "android-arm64",
            "--target", "android-ia32",
            "--target", "android-x64",
            "--linked",
            "--out", "app/src/main/assets/app.bundle",
            "app/src/main/js/app.js"
    ])
}

tasks.register("packPush", Exec) {
    workingDir ".."

    commandLine([
            "node_modules/.bin/bare-pack",
            "--target", "android-arm",
            "--target", "android-arm64",
            "--target", "android-ia32",
            "--target", "android-x64",
            "--linked",
            "--out", "app/src/main/assets/push.bundle",
            "app/src/main/js/push.js"
    ])
}

tasks.register("pack") {
    dependsOn packApp, packPush
}

preBuild.dependsOn link, pack

dependencies {
    api fileTree(dir: "libs", include: ["bare-kit/classes.jar"])
//    implementation platform("com.google.firebase:firebase-bom:33.10.0")
//    implementation "com.google.firebase:firebase-messaging"
    implementation "org.ramani-maps:ramani-maplibre:0.9.0"
    implementation "androidx.compose.material3:material3:1.3.1"
    implementation "androidx.compose.ui:ui:1.7.6"
    implementation "androidx.compose.ui:ui-tooling-preview:1.7.6"
    implementation "androidx.activity:activity-compose:1.9.3"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0"
    implementation("io.insert-koin:koin-core:4.0.3")
    implementation("io.insert-koin:koin-android:4.0.3")
    implementation("io.insert-koin:koin-compose:4.0.3")
    implementation("io.insert-koin:koin-androidx-compose-navigation:4.0.3")

}